C     Last change:  ERB  22 Oct 2002    2:04 pm
      SUBROUTINE GWF1SEEP6ALP(ISUM,LCSEEP,MXSEEP,NSEEP,IN,IOUT,ISEEPCB,
     1        NSEEPVL,ISEEPAL,IFREFM,NPSEEP,ISEEPPB,NNPSEEP,NOPRDR)
C
C-----VERSION 11JAN2000 GWF1DRN6ALP
C     ******************************************************************
C     ALLOCATE ARRAY STORAGE FOR SEEPAGE
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      COMMON /SEEPCOM/SEEPAUX(5)
      CHARACTER*16 SEEPAUX
      CHARACTER*200 LINE
C     ------------------------------------------------------------------
C
C1------IDENTIFY PACKAGE AND INITIALIZE NSEEP.
      WRITE(IOUT,2)
    2 FORMAT(1X,1X,
     1        ' SEEP6 -- SEEPAGE PACKAGE, VERSION 1, 23/9/2000',
     &        'INPUT READ FROM UNIT blabla')
      NSEEP=0
      NNPSEEP=0
      NOPRDR=0
C
C2------READ MAXIMUM NUMBER OF SEEPAGE AND UNIT OR FLAG FOR
C2------CELL-BY-CELL FLOW TERMS.
      CALL URDCOM(IN,IOUT,LINE)
      CALL UPARLSTAL(IN,IOUT,LINE,NPSEEP,MXPS)
      IF(IFREFM.EQ.0) THEN
         READ(LINE,'(2I10)') MXACTS,ISEEPCB
         LLOC=21
      ELSE
         LLOC=1
         CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,MXACTS,R,IOUT,IN)
         CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,ISEEPCB,R,IOUT,IN)
      END IF
      WRITE(IOUT,3) MXACTS
    3 FORMAT(1X,'MAXIMUM OF ',I6,' ACTIVE SEEPAGE AT ONE TIME')
      IF(ISEEPCB.LT.0) WRITE(IOUT,7)
    7 FORMAT(1X,'CELL-BY-CELL FLOWS WILL BE PRINTED WHEN ICBCFL NOT 0')
         IF(ISEEPCB.GT.0) WRITE(IOUT,8) ISEEPCB
    8 FORMAT(1X,'CELL-BY-CELL FLOWS WILL BE SAVED ON UNIT ',I4)
C
C3------READ AUXILIARY VARIABLES AND CBC ALLOCATION OPTION.
      ISEEPAL=0
      NAUX=0
   10 CALL URWORD(LINE,LLOC,ISTART,ISTOP,1,N,R,IOUT,IN)
      IF(LINE(ISTART:ISTOP).EQ.'CBCALLOCATE' .OR.
     1   LINE(ISTART:ISTOP).EQ.'CBC') THEN
         ISEEPAL=1
         WRITE(IOUT,11)
   11    FORMAT(1X,'MEMORY IS ALLOCATED FOR CELL-BY-CELL BUDGET TERMS')
         GO TO 10
      ELSE IF(LINE(ISTART:ISTOP).EQ.'AUXILIARY' .OR.
     1        LINE(ISTART:ISTOP).EQ.'AUX') THEN
         CALL URWORD(LINE,LLOC,ISTART,ISTOP,1,N,R,IOUT,IN)
         IF(NAUX.LT.5) THEN
            NAUX=NAUX+1
            SEEPAUX(NAUX)=LINE(ISTART:ISTOP)
            WRITE(IOUT,12) SEEPAUX(NAUX)
   12       FORMAT(1X,'AUXILIARY SEEPAGE VARIABLE: ',A)
         END IF
         GO TO 10
      ELSE IF(LINE(ISTART:ISTOP).EQ.'NOPRINT') THEN
         WRITE(IOUT,13)
   13    FORMAT(1X,'LISTS OF SEEPAGE CELLS WILL NOT BE PRINTED')
         NOPRDR = 1
         GO TO 10
      END IF
      NSEEPVL=5+NAUX+ISEEPAL
C
C4------ALLOCATE SPACE IN THE RX ARRAY FOR THE SEEPAGE ARRAY.
      ISEEPPB=MXACTS+1
      MXSEEP=MXACTS+MXPS
      ISOLD=ISUM
      LCSEEP=ISUM
      ISUM=ISUM+NSEEPVL*MXSEEP
C
C5------PRINT AMOUNT OF SPACE USED BY SEEPAGE PACKAGE.
      ISP=ISUM-ISOLD
      WRITE (IOUT,14)ISP
   14 FORMAT(1X,I10,' ELEMENTS IN RX ARRAY ARE USED BY SEEP')
C
C6------RETURN.
      RETURN
      END
      SUBROUTINE GWF1SEEP6RPPD(IN,IOUT,NSEEPVL,ISEEPAL,NCOL,NROW,NLAY,
     1            NPSEEP,SEEP,ISEEPPB,MXSEEP,IFREFM,ITERP,INAMLOC,
     2            NOPRSE)
C
C-----VERSION 20130923 GWF1SEEP6RPPD
C     ******************************************************************
C     READ SEEPAGE PARAMETERS
C     ******************************************************************
C
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      DIMENSION SEEP(NSEEPVL,MXSEEP)
      COMMON /SEEPCOM/SEEPAUX(5)
      CHARACTER*16 SEEPAUX
C     ------------------------------------------------------------------
C
C-------READ NAMED PARAMETERS.
      IF (ITERP.EQ.1) WRITE(IOUT,1000) NPSEEP
 1000 FORMAT(1X,//1X,I5,' Seepage parameters')
      IF(NPSEEP.GT.0) THEN
        NAUX=NSEEPVL-5-ISEEPAL
        LSTSUM=ISEEPPB
        ITERPU = ITERP
        IF (NOPRDR .EQ.1) ITERPU = 99
        DO 20 K=1,NPSEEP
          LSTBEG=LSTSUM
          CALL UPARLSTRP(LSTSUM,MXSEEP,IN,IOUT,IP,'SEEP','SEEP',ITERP,
     &                   NUMINST,INAMLOC)
          NLST=LSTSUM-LSTBEG
          IF (NUMINST.GT.1) NLST = NLST/NUMINST
C         ASSIGN STARTING INDEX FOR READING INSTANCES
          IF (NUMINST.EQ.0) THEN
            IB=0
          ELSE
            IB=1
          ENDIF
C         READ LIST(S) OF CELLS, PRECEDED BY INSTANCE NAME IF NUMINST>0
          LB=LSTBEG
          DO 10 I=IB,NUMINST
            IF (I.GT.0) THEN
              CALL UINSRP(I,IN,IOUT,IP,ITERP)
            ENDIF
            CALL ULSTRD(NLST,SEEP,LB,NSEEPVL,MXSEEP,ISEEPAL,IN,IOUT,
     &      'SEEP. NO.  LAYER   ROW   COL     SEEP EL.  STRESS FACTOR',
     &        SEEPAUX,5,NAUX,IFREFM,NCOL,NROW,NLAY,5,5,ITERPU)
            LB=LB+NLST
   10     CONTINUE
   20   CONTINUE
      END IF
C
C6------RETURN
      RETURN
      END
      SUBROUTINE GWF1SEEP6RPSS(SEEP,NSEEP,MXSEEP,IN,IOUT,NSEEPVL,
     1            ISEEPAL,IFREFM,NCOL,NROW,NLAY,NNPSEEP,NPSEEP,ISEEPPB,
     2            NOPRSE)
C
C-----VERSION 11JAN2000 GWF1SEEP6RPSS
C     ******************************************************************
C     READ SEEPAGE HEAD, CONDUCTANCE AND BOTTOM ELEVATION
C     ******************************************************************
C
C     SPECIFICATIONS:
C     ------------------------------------------------------------------
      DIMENSION SEEP(NSEEPVL,MXSEEP)
      COMMON /SEEPCOM/SEEPAUX(5)
      CHARACTER*16 SEEPAUX
C     ------------------------------------------------------------------
C
C1------READ ITMP (NUMBER OF SEEPAGE CELLS OR FLAG TO REUSE DATA) AND
C1------NUMBER OF PARAMETERS.
      IF(NPSEEP.GT.0) THEN
         IF(IFREFM.EQ.0) THEN
            READ(IN,'(2I10)') ITMP,NP
         ELSE
            READ(IN,*) ITMP,NP
         END IF
      ELSE
         NP=0
         IF(IFREFM.EQ.0) THEN
            READ(IN,'(I10)') ITMP
         ELSE
            READ(IN,*) ITMP
         END IF
      END IF
C
C------CALCULATE SOME CONSTANTS
      NAUX=NSEEPVL-5-ISEEPAL
      ITERPU = 1
      IOUTU = IOUT
      IF (NOPRSE.EQ.1) THEN
        ITERPU = 99
        IOUTU = -IOUT
      ENDIF
C
C2------DETERMINE THE NUMBER OF NON-PARAMETER SEEPAGE CELLS.
      IF(ITMP.LT.0) THEN
         WRITE(IOUT,7)
    7    FORMAT(1X,/1X,
     1        'REUSING NON-PARAMETER SEEPAGE FROM LAST STRESS PERIOD')
      ELSE
         NNPSEEP=ITMP
      END IF
C
C3------IF THERE ARE NEW NON-PARAMETER SEEPAGE, READ THEM.
      MXACTS=ISEEPPB-1
      IF(ITMP.GT.0) THEN
         IF(NNPSEEP.GT.MXACTS) THEN
            WRITE(IOUT,99) NNPSEEP,MXACTS
   99       FORMAT(1X,/1X,'THE NUMBER OF ACTIVE SEEPAGE (',I6,
     1                     ') IS GREATER THAN MXACTS(',I6,')')
            CALL USTOP(' ')
         END IF
         CALL ULSTRD(NNPSEEP,SEEP,1,NSEEPVL,MXSEEP,ISEEPAL,IN,IOUT,
     1     'SEEP. NO.  LAYER   ROW   COL     SEEP. EL.  CONDUCTANCE',
     2     SEEPAUX,5,NAUX,IFREFM,NCOL,NROW,NLAY,5,5,ITERPU)
      END IF
      NSEEP=NNPSEEP
C
C1C-----IF THERE ARE ACTIVE SEEP PARAMETERS, READ THEM AND SUBSTITUTE
      CALL PRESET('SEEP')
      IF(NP.GT.0) THEN
         NREAD=NSEEPVL-ISEEPAL
         DO 30 N=1,NP
         CALL UPARLSTSUB(IN,'SEEP',IOUTU,'SEEP',SEEP,NSEEPVL,MXSEEP,
     1                NREAD,MXACTS,NSEEP,5,5,
     2     'SEEP. NO.  LAYER   ROW   COL     SEEP. EL.  CONDUCTANCE',
     3            SEEPAUX,5,NAUX)
   30    CONTINUE
      END IF
C
C3------PRINT NUMBER OF SEEP. CELLS IN CURRENT STRESS PERIOD.
      WRITE (IOUT,101) NSEEP
  101 FORMAT(1X,/1X,I6,' SEEPAGE CELLS')
C
C8------RETURN.
  260 RETURN
      END
      SUBROUTINE GWF1SEEP6FM(NSEEP,MXSEEP,SEEP,HNEW,HCOF,RHS,IBOUND,
     1              NCOL,NROW,NLAY,NSEEPVL)
C
C-----VERSION 23SEPT2013 DSEEPRN6FM
C     ******************************************************************
C     ADD SEEPAGE FLOW TO SOURCE TERM
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      DOUBLE PRECISION HNEW,EEL
C
      DIMENSION SEEP(NSEEPVL,MXSEEP),HNEW(NCOL,NROW,NLAY),
     1         RHS(NCOL,NROW,NLAY),IBOUND(NCOL,NROW,NLAY),
     1         HCOF(NCOL,NROW,NLAY)
C     ------------------------------------------------------------------
C
C1------IF NSEEP<=0 THERE ARE NO SEEPAGE CELLS. RETURN.

      IF(NSEEP.LE.0) RETURN
C
C2------PROCESS EACH CELL IN THE SEEPAGE LIST.
      DO 100 L=1,NSEEP
C
C3------GET COLUMN, ROW AND LAYER OF CELL CONTAINING SEEPAGE.
      IL=SEEP(1,L)
      IR=SEEP(2,L)
      IC=SEEP(3,L)
C
C4-------IF THE CELL IS EXTERNAL SKIP IT.
      IF(IBOUND(IC,IR,IL).LE.0) GO TO 100
C
C5-------IF THE CELL IS INTERNAL GET THE SEEPAGE DATA.
      EL=SEEP(4,L)
      EEL=EL
C      WRITE(*,*) 'HNEW',HNEW(IC,IR,IL)
C
C6------IF HEAD IS LOWER THAN SEEPAGE THEN SKIP THIS CELL.
C      WRITE(*,*) 'HNEW = ', HNEW(IC,IR,IL), ' AND EEL = ', EEL
      IF(HNEW(IC,IR,IL).LE.EEL) GO TO 100
C
C7------HEAD IS HIGHER THAN SEEPAGE. ADD TERMS TO RHS AND HCOF.
C      HNEW(IC,IR,IL)=EEL ! added 26/09/2013
      C=SEEP(5,L)
C      WRITE(*,*)'HCOF(IC,IR,IL)=', HCOF(IC,IR,IL)
C      WRITE(*,*)'C=', C
      HCOF(IC,IR,IL)=HCOF(IC,IR,IL)-C
      RHS(IC,IR,IL)=RHS(IC,IR,IL)-C*EL
  100 CONTINUE
C
C8------RETURN.
      RETURN
      END
      SUBROUTINE GWF1SEEP6BD(NSEEP,MXSEEP,VBNM,VBVL,MSUM,SEEP,DELT,HNEW,
     1        NCOL,NROW,NLAY,IBOUND,KSTP,KPER,ISEEPCB,ICBCFL,BUFF,IOUT,
     2        PERTIM,TOTIM,NSEEPVL,ISEEPAL,IAUXSV)
C-----VERSION 23SEPT2013 GWF1SEEP6BD
C     ******************************************************************
C     CALCULATE VOLUMETRIC BUDGET FOR SEEPAGE
C     ******************************************************************
C
C        SPECIFICATIONS:
C     ------------------------------------------------------------------
      COMMON /SEEPCOM/SEEPAUX(5)
      CHARACTER*16 SEEPAUX
      CHARACTER*16 VBNM(MSUM),TEXT
      DOUBLE PRECISION HNEW,HHNEW,EEL,CC,CEL,RATOUT,QQ
C
      DIMENSION VBVL(4,MSUM),SEEP(NSEEPVL,MXSEEP),HNEW(NCOL,NROW,NLAY),
     1          IBOUND(NCOL,NROW,NLAY),BUFF(NCOL,NROW,NLAY)
C
      DATA TEXT /'      DTM SEEPAGE'/
C     ------------------------------------------------------------------
C
C1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
C1------ACCUMULATOR (RATOUT).
      ZERO=0.
      RATOUT=ZERO
      IBD=0
      IF(ISEEPCB.LT.0 .AND. ICBCFL.NE.0) IBD=-1
      IF(ISEEPCB.GT.0) IBD=ICBCFL
      IBDLBL=0
C
C2------IF CELL-BY-CELL FLOWS WILL BE SAVED AS A LIST, WRITE HEADER.
      IF(IBD.EQ.2) THEN
         NAUX=NSEEPVL-5-ISEEPAL
         IF(IAUXSV.EQ.0) NAUX=0
         CALL UBDSV4(KSTP,KPER,TEXT,NAUX,SEEPAUX,ISEEPCB,NCOL,NROW,NLAY,
     1          NSEEP,IOUT,DELT,PERTIM,TOTIM,IBOUND)
      END IF
C
C3------CLEAR THE BUFFER.
      DO 50 IL=1,NLAY
      DO 50 IR=1,NROW
      DO 50 IC=1,NCOL
      BUFF(IC,IR,IL)=ZERO
50    CONTINUE
C
C4------IF THERE IS NO SEEPAGE THEN DO NOT ACCUMULATE SEEPAGE FLOW.
      IF(NSEEP.LE.0) GO TO 200
C
C5------LOOP THROUGH EACH SEEPAGE CALCULATING FLOW.
      DO 100 L=1,NSEEP
C
C5A-----GET LAYER, ROW & COLUMN OF CELL CONTAINING REACH.
      IL=SEEP(1,L)
      IR=SEEP(2,L)
      IC=SEEP(3,L)
      Q=ZERO
C
C5B-----IF CELL IS NO-FLOW OR CONSTANT-HEAD, IGNORE IT.
      IF(IBOUND(IC,IR,IL).LE.0) GO TO 99
C
C5C-----GET SEEPAGE PARAMETERS FROM SEEPAGE LIST.
      EL=SEEP(4,L)
      EEL=EL
      C=SEEP(5,L)
      HHNEW=HNEW(IC,IR,IL)
C
C5D-----IF HEAD HIGHER THAN seepage, CALCULATE Q=C*(EL-HHNEW).
C5D-----SUBTRACT Q FROM RATOUT.
C5D-----AND PUT HNEW = EEL ? as in Batelaan seepage package?
      IF(HHNEW.GT.EEL) THEN
         CC=C
         CEL=C*EL
         QQ=CEL - CC*HHNEW
         Q=QQ
         RATOUT=RATOUT-QQ
      END IF
C
C5E-----PRINT THE INDIVIDUAL RATES IF REQUESTED(ISEEPCB<0).
      IF(IBD.LT.0) THEN
         IF(IBDLBL.EQ.0) WRITE(IOUT,61) TEXT,KPER,KSTP
   61    FORMAT(1X,/1X,A,'   PERIOD ',I4,'   STEP ',I3)
         WRITE(IOUT,62) L,IL,IR,IC,Q
   62    FORMAT(1X,'SEEP. ',I6,'   LAYER ',I3,'   ROW ',I5,'   COL ',I5,
     1       '   RATE ',1PG15.6)
         IBDLBL=1
      END IF
C
C5F-----ADD Q TO BUFFER.
      BUFF(IC,IR,IL)=BUFF(IC,IR,IL)+Q
C
C5G-----IF SAVING CELL-BY-CELL FLOWS IN A LIST, WRITE FLOW.  OR IF
C5G-----RETURNING THE FLOW IN THE SEEP ARRAY, COPY FLOW TO SEEP.
   99 IF(IBD.EQ.2) CALL UBDSVB(ISEEPCB,NCOL,NROW,IC,IR,IL,Q,
     1                  SEEP(1,L),NSEEPVL,NAUX,6,IBOUND,NLAY)
      IF(ISEEPAL.NE.0) SEEP(NSEEPVL,L)=Q
  100 CONTINUE
C
C6------IF CELL-BY-CELL FLOW WILL BE SAVED AS A 3-D ARRAY,
C6------CALL UBUDSV TO SAVE THEM.
      IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,ISEEPCB,BUFF,NCOL,NROW,
     1                          NLAY,IOUT)
C
C7------MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
  200 ROUT=RATOUT
      VBVL(3,MSUM)=ZERO
      VBVL(4,MSUM)=ROUT
      VBVL(2,MSUM)=VBVL(2,MSUM)+ROUT*DELT
      VBNM(MSUM)=TEXT
C
C8------INCREMENT BUDGET TERM COUNTER.
      MSUM=MSUM+1
C
C9------RETURN.
      RETURN
      END
